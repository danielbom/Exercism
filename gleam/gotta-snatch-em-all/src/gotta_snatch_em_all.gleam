import gleam/list
import gleam/set.{type Set}
import gleam/string

pub fn new_collection(card: String) -> Set(String) {
  set.from_list([card])
}

pub fn add_card(collection: Set(String), card: String) -> #(Bool, Set(String)) {
  #(set.contains(collection, card), set.insert(collection, card))
}

pub fn trade_card(
  my_card: String,
  their_card: String,
  collection: Set(String),
) -> #(Bool, Set(String)) {
  let is_possible_and_worthy =
    set.contains(collection, my_card) && !set.contains(collection, their_card)
  let collection = collection |> set.insert(their_card) |> set.delete(my_card)
  #(is_possible_and_worthy, collection)
}

pub fn boring_cards(collections: List(Set(String))) -> List(String) {
  case collections {
    [] -> []
    [head, ..tail] -> list.fold(tail, head, set.intersection) |> set.to_list()
  }
}

pub fn total_cards(collections: List(Set(String))) -> Int {
  collections
  |> list.fold(set.new(), set.union)
  |> set.size()
}

pub fn shiny_cards(collection: Set(String)) -> Set(String) {
  collection
  |> set.filter(fn(it) { string.starts_with(it, "Shiny ") })
}
